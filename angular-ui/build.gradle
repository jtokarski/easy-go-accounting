import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
}

group = 'org.defendev.easygo'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def npmInstall = tasks.register('npmInstall', org.gradle.api.tasks.Exec) {
    onlyIf {
        def skipNpmInstall = Boolean.parseBoolean(providers.gradleProperty('skipNpmInstall').getOrElse(''))
        !skipNpmInstall
    }
    workingDir "${project.projectDir}/workspace"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm install'
    } else {
        executable 'npm'
        args 'install'
    }
}

def angularBuildTask = tasks.register('angularBuild', org.gradle.api.tasks.Exec) {
    dependsOn npmInstall
    onlyIf {
        def skipAngularBuild = Boolean.parseBoolean(providers.gradleProperty('skipAngularBuild').getOrElse(''))
        !skipAngularBuild
    }
    workingDir "${project.projectDir}/workspace"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        println 'Building Angular project on Windows OS'
        commandLine 'cmd', '/c', 'npm run-script ng -- run efa-app:build:production'
    } else {
        println 'Building Angular project on *nix OS'
        executable 'npm'
        args 'run-script', 'ng', '--', 'run', 'efa-app:build:production'
    }
}

processResources {
    dependsOn angularBuildTask
    from('workspace/angular-dist') {
        into 'view-resources'
    }
    from('static') {
        into 'view-resources/static'
    }
}

