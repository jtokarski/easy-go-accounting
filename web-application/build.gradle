
plugins {
    id 'java'
    id 'war'
    alias libs.plugins.springDependencyManagementPlugin
    alias libs.plugins.springBootPlugin
    alias libs.plugins.gitPropertiesPlugin
}

group = 'org.defendev'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

springBoot {
    mainClass = 'org.defendev.easygo.web.EasygoWebApplication'
    buildInfo()
}

bootWar {
    enabled = false
}

tasks.named('bootRun') {
    def runJarConfig = project.rootDir.toPath().resolve('config/application-runJar.yaml').toString()
    systemProperty 'spring.config.additional-location', runJarConfig
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs += ['-Xlint:deprecation']
//}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    modules {
        module("org.springframework.boot:spring-boot-starter-logging") {
            replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
        }
    }
    /*
     * Specifying the 'spring-boot-starter-tomcat' in providedRuntime configuration
     * doesn't affect the contents boot-War as the tomcat-embed-* Jars will be
     * included in boot-War anyway.
     * Rather, it does affect the plain-War causing the tomcat-embed-* Jars
     * to be excluded from the archive (saving around 4MB of storage).
     *
     */
    providedRuntime group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'
    implementation group: 'com.oracle.database.jdbc', name: 'ojdbc11'
    implementation libs.guava
    implementation libs.defendevCommonJdk
    implementation libs.defendevCommonSpring6
    implementation project(':domain-financial-accounting-api')
    implementation project(':angular-ui')

    // todo: convert to runtimeOnly
    implementation project(':domain-financial-accounting')

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.springframework.security', name: 'spring-security-test'
}

test {
    useJUnitPlatform()
}
